set datafile separator comma
cd 'C:\Users\gbonneau\git\smallz4'
corpusFile = "lz4_silicia_corpus.txt_4096.csv"
stats corpusFile nooutput
numRecord = STATS_records
chunkSize = numRecord-15
print numRecord
print chunkSize

array M[numRecord]
array N[numRecord]

stats corpusFile using (M[int($0+1)] = $1) name "M" nooutput
stats corpusFile using (N[int($0+1)] = $2) name "N" nooutput
array M_x_N[numRecord]

stats N using (M_x_N[int($0+1)] = N[int($0+1)]*M[int($0+1)]) name "M_x_N" nooutput

compMeanSize = M_x_N_sum / N_sum
compMeanRatio = chunkSize / compMeanSize

array M_mean_x_N_sqrt[numRecord]
stats M using (M_mean_x_N_sqrt[int($0+1)] = N[int($0+1)]*((($1)-compMeanSize)**2)) name "M_mean_x_N_2" nooutput

compVarianceSize = sqrt(M_mean_x_N_2_sum/(N_sum-1))
compVarianceHigh = chunkSize/(compMeanSize - compVarianceSize)
compVarianceLow = chunkSize/(compMeanSize + compVarianceSize)

posMedian = M_x_N_sum/2
posUpQuartile = M_x_N_sum/4
posLowQuartile = posMedian + posUpQuartile

isMedian          = 0
isUpQuartile      = 0
isLowQuartile     = 0
isLastMedian      = 0
isLastLowQuartile = 0
isLastUpQuartile  = 0

medianValue       = 0
upQuartileValue   = 0
lowQuartileValue  = 0

cumTotal          = 0

array cumulMedian[numRecord]

do for [col=1:numRecord] {

   cumulMedian[col] = cumTotal + M_x_N[col]
   cumTotal = cumTotal + M_x_N[col]

   isMedian = cumulMedian[col] < posMedian ? 0 : 1 
   isLowQuartile = cumulMedian[col] < posLowQuartile ? 0 : 1
   isUpQuartile = cumulMedian[col] < posUpQuartile ? 0 : 1

   if(isUpQuartile != isLastUpQuartile) {
      upQuartileValue = col
   }

   if(isMedian != isLastMedian) {
      medianValue = col
   }

   if(isLowQuartile != isLastLowQuartile) {
      lowQuartileValue = col
      break
   }

   isLastMedian = isMedian
   isLastLowQuartile = isLowQuartile
   isLastUpQuartile = isUpQuartile
}

ratioMedian = chunkSize/medianValue
ratioUpQuartile = chunkSize/upQuartileValue
ratioLowQuartile = chunkSize/lowQuartileValue

print medianValue
print upQuartileValue
print lowQuartileValue

array Candle[6]

Candle[1] = 1
Candle[2] = 1
Candle[3] = upQuartileValue
Candle[4] = medianValue
Candle[5] = lowQuartileValue
Candle[6] = chunkSize

array V[N_sum]
numSample = 1

do for [col=1:numRecord] {
   if(N[col] != 0) {
      do for [num=1:(N[col])] {      
         V[numSample] = M[col] 
         numSample=numSample+1
      }
   }
}

set table $Data separator comma
    set samples numSample-1
    n = 0
    plot for [i=1:1] '+' u (n=n+1):(V[n]) w table
unset table

set table $kdensity
    set samples (numSample-1)*3
    plot $Data using 2:(1) smooth kdensity bandwidth 10.
unset table

set datafile separator whitespace
set xrange[-50:50]
set yrange[0:5000]

set datafile separator whitespace
set key noautotitle
set style fill solid 0.7
set boxwidth 2.5

plot $kdensity using (1 + $2/1.):1 w filledcurves x=1 lt 10, '' using (1 - $2/1.):1 w filledcurves x=1 lt 2, \
Candle every ::::0 using (Candle[1]):(Candle[3]):(Candle[2]):(Candle[6]):(Candle[5]) with candlesticks fs solid lt 1 lw 2 notitle whiskerbars, \
Candle every ::::0 using (Candle[1]):(Candle[4]):(Candle[4]):(Candle[4]):(Candle[4]) with candlesticks fs solid lt -1 lw 2 notitle whiskerbars






